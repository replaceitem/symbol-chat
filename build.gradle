plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.minecraft_version + '-' + project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases"}
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/symbol-chat.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    
    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
//    modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
    
    modImplementation "maven.modrinth:reconfigure:${project.reconfigure_version}"
    include "maven.modrinth:reconfigure:${project.reconfigure_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "reconfigure_version", project.reconfigure_version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version, "reconfigure_version": inputs.properties.reconfigure_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesBaseName}"}
    }
}


import com.modrinth.minotaur.dependencies.DependencyType
import com.modrinth.minotaur.dependencies.ModDependency

modrinth {
    token = System.getenv("MODRINTH_TOKEN") // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
    projectId = "NKvLVQMc"
    versionNumber = project.mod_version // You don't need to set this manually. Will fail if Modrinth has this version already
    versionType = "release" // This is the default
    uploadFile = remapJar // With Fabric Loom or Architectury Loom, this MUST be set to `remapJar` instead of `jar`!
    loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Fabric Loom or ForgeGradle
    dependencies = [
            // fabric api needed
            new ModDependency("P7dR8mSH", DependencyType.REQUIRED),
            // modmenu optional for accessing the configuration
            new ModDependency("mOgUt4GM", DependencyType.OPTIONAL),
            // reconfigure bundled
            new ModDependency("QHmQeeNt", DependencyType.EMBEDDED)
    ]
    changelog = rootProject.file("changelog.md").text
    syncBodyFrom = rootProject.file("README.md").text
}
